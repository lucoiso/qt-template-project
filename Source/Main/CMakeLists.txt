# Copyright Notices: [...]

# ----------- Global Definitions -----------
SET(LIBRARY_NAME ${PROJECT_NAME})


# ------------- Library Setup --------------
QT_ADD_EXECUTABLE(${LIBRARY_NAME} MANUAL_FINALIZATION ${CMAKE_CURRENT_SOURCE_DIR}/Source/Main.cpp)

QT_ADD_QML_MODULE(${LIBRARY_NAME}
        URI ${LIBRARY_NAME}
        VERSION 1.0
        QML_FILES Source/Main.qml
        NO_PLUGIN
        RESOURCE_PREFIX /
)

FIND_PACKAGE(Qt${QT_VERSION_MAJOR} REQUIRED COMPONENTS QuickTemplates2 OpenGL Qml)

TARGET_LINK_LIBRARIES(${LIBRARY_NAME} PRIVATE
        Application
)

SET_TARGET_PROPERTIES(${LIBRARY_NAME} PROPERTIES
        MACOSX_BUNDLE_GUI_IDENTIFIER com.example.${LIBRARY_NAME}
        MACOSX_BUNDLE_BUNDLE_VERSION ${PROJECT_VERSION}
        MACOSX_BUNDLE_SHORT_VERSION_STRING ${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}
        MACOSX_BUNDLE TRUE
        WIN32_EXECUTABLE TRUE
)

QT_FINALIZE_TARGET(${LIBRARY_NAME})


# ---------------- Commands ----------------
INSTALL(FILES $<TARGET_RUNTIME_DLLS:${LIBRARY_NAME}> TYPE BIN)

SET(QT_CUSTOM_ARGUMENTS "")
LIST(APPEND QT_CUSTOM_ARGUMENTS --dir ${CMAKE_CURRENT_BINARY_DIR})
LIST(APPEND QT_CUSTOM_ARGUMENTS --force)
LIST(APPEND QT_CUSTOM_ARGUMENTS --no-system-d3d-compiler)
LIST(APPEND QT_CUSTOM_ARGUMENTS --no-system-dxc-compiler)
LIST(APPEND QT_CUSTOM_ARGUMENTS --no-opengl-sw)
LIST(APPEND QT_CUSTOM_ARGUMENTS --no-ffmpeg)
LIST(APPEND QT_CUSTOM_ARGUMENTS --verbose 2)

QT_GENERATE_DEPLOY_QML_APP_SCRIPT(TARGET ${LIBRARY_NAME}
        OUTPUT_SCRIPT
        QT_DEPLOY_SCRIPT
        NO_UNSUPPORTED_PLATFORM_ERROR
        NO_TRANSLATIONS
        NO_COMPILER_RUNTIME
        DEPLOY_USER_QML_MODULES_ON_UNSUPPORTED_PLATFORM
        MACOS_BUNDLE_POST_BUILD
        DEPLOY_TOOL_OPTIONS ${QT_CUSTOM_ARGUMENTS}
)
INSTALL(SCRIPT ${QT_DEPLOY_SCRIPT})
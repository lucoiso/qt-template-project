# Copyright Notices: [...]

# ----------- Global Definitions -----------
SET(LIBRARY_NAME UserInterface)

# ------------- Library Setup --------------
SET(PRIVATE_MODULES_BASE_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/Source)
SET(PUBLIC_MODULES_BASE_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/Include/${LIBRARY_NAME})

FILE(GLOB_RECURSE PRIVATE_MODULES "${PRIVATE_MODULES_BASE_DIRECTORY}/*.cpp")
FILE(GLOB_RECURSE PUBLIC_MODULES "${PUBLIC_MODULES_BASE_DIRECTORY}/*.hpp")

# ---------------- Qt Setup ----------------
FIND_PACKAGE(Qt${QT_VERSION_MAJOR} REQUIRED COMPONENTS Qml Quick QuickControls2)

SET(QML_FILES
        MainWindow.qml
        Components/ApplicationShell.qml
        Pages/HomePage.qml
        Pages/LoginPage.qml
        Pages/PlaceholderPage.qml
)

QT_ADD_LIBRARY(${LIBRARY_NAME} STATIC)

QT_ADD_QML_MODULE(${LIBRARY_NAME}
        URI ${LIBRARY_NAME}
        VERSION 1.0
        QML_FILES ${QML_FILES}
        SOURCES ${PRIVATE_MODULES} ${PUBLIC_MODULES}
        RESOURCE_PREFIX /
)

TARGET_LINK_LIBRARIES(${LIBRARY_NAME} PRIVATE
        Qt${QT_VERSION_MAJOR}::Core
        Qt${QT_VERSION_MAJOR}::Qml
        Qt${QT_VERSION_MAJOR}::QuickControls2
)

TARGET_INCLUDE_DIRECTORIES(${LIBRARY_NAME} PRIVATE
        ${PRIVATE_MODULES_BASE_DIRECTORY}
        ${PUBLIC_MODULES_BASE_DIRECTORY}
)

TARGET_INCLUDE_DIRECTORIES(${LIBRARY_NAME} PUBLIC ${PUBLIC_MODULES_BASE_DIRECTORY}/..)